#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("МассивСубконтоПДР", МассивСубконтоПДР);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ПрочиеДоходыРасходы_НеОблагаетсяНДС" Тогда
		Если Модифицированность Тогда
			Возврат;
		КонецЕсли;
		
		ЗаполнитьПоУмолчаниюНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Закрыть(МассивСубконтоПДР);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоУмолчанию(Команда)
	
	ЗаполнитьПоУмолчаниюНаСервере();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОписаниеПоляПрочиеДоходы()

	Возврат ЗаполнениеРасшифровкаРегламентированнойОтчетности.ОписаниеСубконтоСчета(
		"Субконто",
		ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ПрочиеДоходы"),
		ПредопределенноеЗначение("ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы"));

КонецФункции 

&НаКлиенте
Процедура ОСВПо91(Команда)
	
	// Подготавливаем структуру для отбора необходимых параметров.
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	
	ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
	ДополнительныеСвойства.Вставить("НачалоПериода", 	НачалоГода(ТекущаяДата()));
	ДополнительныеСвойства.Вставить("КонецПериода", 	КонецГода(ТекущаяДата()));
	ДополнительныеСвойства.Вставить("Счет", 			ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ПрочиеДоходы"));
	
	ОписаниеПоля = ОписаниеПоляПрочиеДоходы();
	Если ОписаниеПоля <> Неопределено Тогда
		СтрокаНастройкиГруппировки = Новый Структура;
		СтрокаНастройкиГруппировки.Вставить("Использование",  Истина);
		СтрокаНастройкиГруппировки.Вставить("Поле",           ОписаниеПоля.Имя);
		СтрокаНастройкиГруппировки.Вставить("Представление",  ОписаниеПоля.Представление);
		СтрокаНастройкиГруппировки.Вставить("ТипГруппировки", 0);
	КонецЕсли;
	
	ГруппировкаРасшифровки = Новый Массив;
	ГруппировкаРасшифровки.Добавить(СтрокаНастройкиГруппировки);
	ДополнительныеСвойства.Вставить("Группировка", ГруппировкаРасшифровки);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("РежимРасшифровки", 		Истина);
	ПараметрыФормы.Вставить("ВидРасшифровки", 			2); // Из пользовательских настроек
	ПараметрыФормы.Вставить("ЗаполняемыеНастройки",		 Новый Структура("Показатели, Группировка, Отбор", Истина, Истина, Ложь));
	ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии",	 Истина);
	
	ОткрытьФорму("Отчет.ОборотноСальдоваяВедомостьПоСчету.Форма", ПараметрыФормы, ЭтотОбъект, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПоУмолчаниюНаСервере()
	
	ЭтотОтчет = РеквизитФормыВЗначение("Отчет");
	МассивСубконто = Справочники.ПрочиеДоходыИРасходы.МассивСубконтоНеОблагаетсяНДС();
	Если МассивСубконто.Количество() = 0 Тогда
		МассивСубконто = Справочники.ПрочиеДоходыИРасходы.МассивСубконтоРазрешенныхПрочихДоходовИРасходов();
	КонецЕсли;
	
	МассивСубконтоПДР.ЗагрузитьЗначения(МассивСубконто);

КонецПроцедуры

#КонецОбласти